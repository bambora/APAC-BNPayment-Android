
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.1.9"

def siteUrl = 'http://bambora.com/en/au'
def gitUrl = 'https://github.com/bambora/APAC-BNPayment-Android.git'

group = "com.bambora.apac"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode = System.getenv("BUILD_NUMBER") as Integer ?: 0
        versionName "1.1.9"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'commons-codec:commons-codec:1.9'
    implementation 'io.card:android-sdk:5.5.1'

    compileOnly project(':VisaCheckoutHybrid')

    testImplementation 'junit:junit:4.12'
    testImplementation 'commons-codec:commons-codec:1.9'
    testImplementation 'org.json:json:20140107'
    testImplementation 'org.mockito:mockito-core:2.28.2'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'org.mockito:mockito-core:2.28.2'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.25.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'The Mobile Payment SDK from Bambora (APAC) makes it simple to accept credit card payments in your app.'
                url siteUrl
                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/bambora/APAC-BNPayment-Android/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'bnpayment'
                        name 'APAC-BNPayment'
                        email 'apac-mobile-sdk-maintainers@bambora.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")
    configurations = ['archives']
    pkg {
        repo = "APAC-BNPayment"
        name = "bn-payment"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}

